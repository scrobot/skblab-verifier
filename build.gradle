plugins {
	id 'org.springframework.boot' version '2.1.7.RELEASE' apply false
	id 'com.bmuschko.docker-spring-boot-application' version '4.6.2' apply false
	id 'com.google.protobuf' version '0.8.10' apply false
}

apply plugin: 'base'

subprojects {
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'
	apply plugin: 'idea'

	repositories {
		jcenter()
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		[enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.1.7.RELEASE'),
		 platform('org.junit:junit-bom:5.4.0'),
		].each {
			implementation it
			annotationProcessor it
			testAnnotationProcessor it
		}

		constraints {
			['org.projectlombok:lombok:1.18.2'].each {
				compileOnly it
				annotationProcessor it
				testAnnotationProcessor it
			}

			['org.beryx:text-io:3.3.0',
			 'org.beryx:text-io-web:3.3.0',
			 'org.codehaus.groovy:groovy-all:2.5.6',
			 'com.google.guava:guava:27.1-jre'
			].each {
				implementation it
			}

			['org.mockito:mockito-core:2.22.0',
			 'org.mockito:mockito-junit-jupiter:2.22.0',
			 'io.github.benas:random-beans:3.7.0',
			].each {
				testImplementation it
			}
		}

		//declare dependencies
		['org.projectlombok:lombok',
		 'org.springframework.boot:spring-boot-configuration-processor',
		].each {
			compileOnly it
			testCompileOnly it
			annotationProcessor it
			testAnnotationProcessor it
		}

		// declare gRPC dependencies
		['io.grpc:grpc-stub:1.21.0',
		'io.grpc:grpc-netty:1.21.0',
		'io.grpc:grpc-okhttp:1.21.0',
		'io.grpc:grpc-protobuf:1.21.0',
		'io.grpc:protoc-gen-grpc-java:1.21.0',
		'com.google.protobuf:protoc:3.9.0',
		'com.salesforce.servicelibs:reactor-grpc-stub:0.10.0',
		'io.github.lognet:grpc-spring-boot-starter:3.3.0'].each {
			implementation it
		}

		// declare spring boot starters
		['org.springframework.boot:spring-boot-starter-webflux',
		 'org.springframework.boot:spring-boot-starter-actuator',
		 'io.projectreactor.addons:reactor-extra',
		 'com.google.guava:guava',
		 'org.beryx:text-io',
		 'org.beryx:text-io-web'].each {
			implementation it
		}

//		runtime "com.h2database:h2"

		testImplementation 'org.mockito:mockito-core',
                "org.assertj:assertj-core:3.11.1",
				'org.mockito:mockito-junit-jupiter',
				'org.junit.jupiter:junit-jupiter-engine',
				'org.springframework.boot:spring-boot-starter-test'
	}

	test {
		useJUnitPlatform()
		testLogging {
			exceptionFormat = 'full'
			events 'passed', 'skipped', 'failed'
		}
		reports {
			html.enabled = true
		}
	}
}
